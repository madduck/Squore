// export ANDROID_HOME=/osshare/software/google/android-sdk-linux
apply plugin: 'com.android.application'
buildscript {
    // required for using gradle i.s.o. gradlew
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'

        // added for firebase to parse google-services.json
        classpath 'com.google.gms:google-services:4.3.14'
    }
}
repositories {
    mavenCentral() // for protobuf
    google()
    jcenter()

    // required to work with aar file in libs dir
    flatDir {
        dirs 'libs'
    }
}
// Run ./gradlew -q :dependencies
android {
    compileOptions {
        // added when going from 1.1.9 to androidx.wear:wear:1.2.0
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    compileSdkVersion 33
    // buildToolsVersion not required for gradle 3.0.0 and higher (e.g. when using wrapper)
    //buildToolsVersion '28.0.3'

    lintOptions {
        disable 'MissingTranslation'
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds, but continue the build even when errors are found:
        abortOnError false
        //deprecation true

        // Normally lint will analyze all dependencies along with each module; this ensures
        // that lint can correctly (for example) determine if a resource declared in a library
        // is unused; checking only the library in isolation would not be able to identify this
        // problem. However, this leads to quite a bit of extra computation; a library is
        // analyzed repeatedly, for each module that it is used in.
        //checkDependencies false
    }
    defaultConfig {
        // must match (or be larger than) minSdkVersion of AndroidBase
        // https://www.androidpolice.com/2018/10/28/android-platform-distribution-numbers-updated-october-pie-still-missing/
        // https://en.wikipedia.org/wiki/Android_version_history
        //minSdkVersion 19 // 4.4 android.os.Build.VERSION_CODES.KITKAT, for wear at least 23 is required
        //minSdkVersion 23 // 6.0 android.os.Build.VERSION_CODES.M, for wear at least 23 is required, wear2.0 needs 25
    }
    sourceSets {
        main {
            manifest {
                // one valid entry must be here in main.manifest
                srcFile 'AndroidManifestSquore.xml'
            }
            java {
                srcDir 'src'
                // exclude '**/cast/**'
                exclude '**/firebase/OneSignal**'
            }
            res {
                srcDir 'res'
                // exclude '**/layout-watch/**' // e.g. for non-wear does not work for resources
            }
        }
        phoneTabletPre22  { manifest { srcFile 'AndroidManifestALLPhoneTabletPre22.xml' } }
        phoneTabletPost23 { manifest { srcFile 'AndroidManifestALLPhoneTablet.xml'      } }
        wearOs            { manifest { srcFile 'AndroidManifestALLWearOS.xml'           } }
    }

    signingConfigs {
        release {
            try {
                storeFile file(KEYSTORE_PATH)
                storePassword  KEYSTORE_PASSWORD
                keyAlias       KEY_ALIAS
                keyPassword    KEY_PASSWORD
            } catch (ex) {
                throw new InvalidUserDataException("Please do one of the following:\n 1) define KEYSTORE_PATH, KEYSTORE_PASSWORD, KEY_ALIAS and KEY_PASSWORD in \$HOME/.gradle/gradle.properties for creating signed apk's. Or, \n 2) Comment out the line 'signingConfig signingConfigs.release' in the file build.gradle to continue with unsigned apk's")
            }
        }
    }
    flavorDimensions "devicetype" // actual flavors use camelCase with first letter lowercase
    productFlavors {
        // versionCodePrefix specifies [min_api_level][bitwise_supported_screensizes s=1,m=2,l=4,xl=8]
        phoneTabletPre22 {
            dimension "devicetype"
            minSdkVersion 19
            targetSdkVersion 33
            multiDexEnabled true // required since using firebase
            versionCode 1915 * 10000 + 495
            versionName "4.50"
        }
        phoneTabletPost23 {
            dimension "devicetype"
            minSdkVersion 23
            targetSdkVersion 33
            versionCode 2315 * 10000 + 495
            versionName "4.50"
        }
        wearOs {
            dimension "devicetype"
            minSdkVersion 25    // 25 for Wear2.0 (23 would suffice for Wear1.0)
            targetSdkVersion 33
            versionCode 2501 * 10000 + 495
            versionName "4.50"
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release // comment out this line if you are just trying a simple build that does need to be signed
            //embedMicroApp true

            //minifyEnabled true // somehow this makes the app crash
            //shrinkResources true // can not be done without minifyEnabled

//            proguardFiles 'proguard-android-optimize.txt',
//                          'proguard-rules.pro'
            //proguardFiles 'proguard-rules.pro'
            //proguardFiles 'proguard-android.txt'
            //proguardFiles 'proguard-android-optimize.txt'
        }
/*
        debug {
            initWith release
            manifestPlaceholders = [hostName:"internal.example.com"]
            applicationIdSuffix ".debug"
            debuggable true
        }
        minified {
            //debuggable true
            minifyEnabled true // somehow this makes my app crash
            // 'shrinkResources=true' currently only helps for 2%
            shrinkResources true
            //proguardFile 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
*/
    }
    aaptOptions {
        ignoreAssetsPattern '_gh_pages'
    }
}

dependencies {

    //implementation project(':AndroidBase')
    implementation 'com.doubleyellow:AndroidBase@aar'
    // uncommenting this while in Android Studio creates a subdir AndroidBase with an .iml file in it

    implementation 'androidx.wear:wear:1.2.0'       // minSdk=23 for 1.[0-2].0
    implementation 'androidx.wear:wear-input:1.1.0' // minSdk=25
    //phoneTabletPre22Implementation  'androidx.wear:wear-input:1.1.0'// comment out this line temporarily what code will lead to errors when invocation is not prevented using Build.VERSION.SDK_INT > Build.VERSION_CODES.?
    //phoneTabletPost23Implementation 'androidx.wear:wear-input:1.1.0'// minSdk=25
    //wearOsImplementation            'androidx.wear:wear-input:1.1.0'

    //wearOsImplementation 'androidx.wear:wear:1.0.0' // needs minSdkVersion=23
    //implementation 'com.android.support:wear:28.0.0' // needs minSdkVersion=23
    //implementation 'com.google.protobuf:protobuf-java:3.5.1'

    implementation fileTree(dir: 'libs', include: ['*.jar']) // AndroidBase and Graph

    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'

    implementation 'androidx.appcompat:appcompat:1.5.1' // moving from 1.3.1 to 1.4.0 makes e.g. android wear in emulator fail
    // not required for building, BUT required for runtime when using newer gradle (20191022)

  //implementation 'androidx.legacy:legacy-support-v13:1.0.0'

    // ChromeCast (new with Custom Receiver)
    phoneTabletPre22Implementation  'com.google.android.gms:play-services-cast-framework:21.2.0'
    phoneTabletPost23Implementation 'com.google.android.gms:play-services-cast-framework:21.2.0'
    wearOsImplementation            'com.google.android.gms:play-services-cast-framework:21.2.0' // wearOs can not cast but to run it still needs 'Implementation' to not have 'ClassNotFound' exceptions

    // allow communication between handheld device and wearable
    phoneTabletPre22CompileOnly       'com.google.android.gms:play-services-wearable:18.0.0' // compileOnly is good enough for API < 23
    phoneTabletPost23Implementation   'com.google.android.gms:play-services-wearable:18.0.0'
    wearOsImplementation              'com.google.android.gms:play-services-wearable:18.0.0'

    phoneTabletPre22CompileOnly       'com.google.android.support:wearable:2.9.0'
    phoneTabletPost23Implementation   'com.google.android.support:wearable:2.9.0'
    wearOsImplementation              'com.google.android.support:wearable:2.9.0'

    // Required when using 'com.google.android.support:wearable:2.9.0' for e.g. com/google/android/gms/wearable/CapabilityClient$OnCapabilityChangedListener
    compileOnly       'com.google.android.wearable:wearable:2.9.0' // compileOnly sufficient for devices with API < 23
  //phoneTabletPre22CompileOnly       'com.google.android.wearable:wearable:2.9.0'
  //phoneTabletPost23CompileOnly      'com.google.android.wearable:wearable:2.9.0'
  //wearOsCompileOnly                 'com.google.android.wearable:wearable:2.9.0'

    //wearApp project(':wear:base') // applies to all other variants

    // Import the Firebase BoM (see: https://firebase.google.com/docs/android/learn-more#bom)
    //implementation platform('com.google.firebase:firebase-bom:30.4.1')
    //implementation 'com.google.firebase:firebase-messaging'
    //implementation 'com.google.firebase:firebase-core'
    //implementation 'com.google.firebase:firebase-iid'
    // For an optimal experience using FCM, add the Firebase SDK for Google Analytics. This is recommended, but not required.
    //implementation 'com.google.firebase:firebase-analytics'

    implementation platform('com.google.firebase:firebase-bom:31.1.1') // 2022-12-17: compiles OK but PushNotifications.start in PusherHandler.init fails with java.lang.ClassNotFoundException: com.google.firebase.iid.FirebaseInstanceId if firebase-iid is not added specifically
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-analytics'     // replaces firebase-core
    implementation 'com.google.firebase:firebase-installations' // replaces firebase-iid
    // https://github.com/pusher/push-notifications-android/issues/117
    implementation 'com.google.firebase:firebase-iid:21.1.0' // add explicitly since no longer in the BOM, but required by com.pusher:push-notifications-android:1.9.0


    // firebase in combination with pusher
    // https://github.com/pusher/push-notifications-android
    implementation 'com.pusher:push-notifications-android:1.9.0'
}

/*
dependencies {
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
}
*/
apply plugin: 'com.google.gms.google-services' // for firebase, comment out for specially branded version of the app that are not in google-services.json
